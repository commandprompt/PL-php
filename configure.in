dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(PL/php, 1.4, plphp@lists.commandprompt.com)
AC_CONFIG_SRCDIR([plphp_io.c])
AC_CONFIG_HEADER([config.h])

# Checks for common programs.
AC_PROG_CC
AC_PROG_AWK

AC_ARG_WITH([php], AC_HELP_STRING([--with-php=DIR], 
								  [specify a path to the PHP installation]), [PHP_PATH=$withval])
AC_ARG_WITH([postgres], AC_HELP_STRING([--with-postgres=DIR], 
									   [specify a path to the PostgreSQL installation]), [PG_PATH=$withval])
AC_ARG_ENABLE([debug], AC_HELP_STRING([--enable-debug],
									  [build with debug information]), [DEBUG="$enableval"  ],[])
# Checks for header files.
AC_CHECK_HEADERS([fcntl.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

# Checks for library functions.
AC_CHECK_FUNCS([strcasecmp strdup])

AC_MSG_CHECKING([whether to build with debug information])
if test "$DEBUG" = "yes"; then
	CPPFLAGS+="-g";
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

# check for pg_config 
AC_PATH_PROG([PG_CONFIG], [pg_config], [], [$PG_PATH:$PG_PATH/bin:$PATH])
if test "$PG_CONFIG" = ""; then
    AC_MSG_ERROR([Cannot locate pg_config])
fi
AC_SUBST(PG_CONFIG)

PGXS=$($PG_CONFIG --pgxs)
AC_CHECK_FILE($PGXS, [], AC_MSG_ERROR([file $PGXS doesn't exist]))
# ' hello text editor
AC_SUBST(PGXS)

# check for PostgreSQL version
AC_MSG_CHECKING([for PostgreSQL version])
PG_VERSION=$($PG_CONFIG --version|$AWK '{ print $2 }')
PG_MAJOR_VERSION=$(echo "$PG_VERSION"|$AWK -F. '{ print $1 }')
PG_MINOR_VERSION=$(echo "$PG_VERSION"|$AWK -F. '{ print substr($2,1,1) }')
AC_MSG_RESULT([$PG_VERSION])

STOP="no"
if test $PG_MAJOR_VERSION -lt 8 ; then
    STOP="yes"
else
    if test "$PG_MAJOR_VERSION" = 8 -a "$PG_MINOR_VERSION" -lt 1 ; then
	STOP="yes"
    fi
fi
if test "$STOP" = "yes"; then
    AC_MSG_ERROR([PosgreSQL 8.1 or newer required])
fi
AC_DEFINE_UNQUOTED(PG_MAJOR_VERSION,$PG_MAJOR_VERSION,[PostgreSQL major version])
AC_DEFINE_UNQUOTED(PG_MINOR_VERSION,$PG_MINOR_VERSION,[PostgreSQL minor version])

# check for php-config    
AC_PATH_PROGS([PHP_CONFIG], [php-config], [], [$PHP_PATH:$PHP_PATH/bin:$PATH])
if test "$PHP_CONFIG" = ""; then
    AC_MSG_ERROR([Cannot locate php-config])
fi

PHP_INCLUDES=$($PHP_CONFIG --includes)
PHP_LDFLAGS=$($PHP_CONFIG --libs)
AC_SUBST(PHP_INCLUDES)
AC_SUBST(PHP_LDFLAGS)
LDFLAGS="$LDFLAGS -L$($PHP_CONFIG --prefix)/lib"

# we need php include files path to check for ZTS
CPPFLAGS="$PHP_INCLUDES $CPPFLAGS"

# checking if ZTS (zend thread safety) is enabled
AC_CHECK_DECL(ZTS, [zts_enabled="yes"], [zts_enabled="no"], [ #include "php_config.h" ]) 
if test "$zts_enabled" = "yes"; then
	AC_MSG_ERROR([PL/php requires non thread-safe PHP build.
Please rebuild your PHP library with thread-safe support disabled])
fi

AC_MSG_CHECKING([whether PHP is compiled with the Embed SAPI])
$PHP_CONFIG --php-sapis | grep -q embed
if test $? == 0; then
	embed_sapi_present=yes
else
	embed_sapi_present=no
fi
AC_MSG_RESULT([$embed_sapi_present])
if test "$embed_sapi_present" = "no"; then
	AC_MSG_ERROR([PL/php requires the Embed PHP SAPI.
Please rebuild your PHP library with --enable-embed])
fi

# Checks for libraries.
AC_CHECK_LIB([php5], [php_module_startup],[], [have_php5="no"], [$PHP_LDFLAGS])
if test "$have_php5" = "no"; then
	AC_MSG_ERROR([Cannot find PHP 5 SAPI library])
fi


AC_CONFIG_FILES([Makefile])
AC_OUTPUT
